terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 4.0"
    }
  }
}

provider "azurerm" {
  features {}
}

# Create a Resource Group
# This is the logical container for all resources. It must be created first.
resource "azurerm_resource_group" "network-rg" {
  name     = "network-rg"
  location = "Canada Central"
}

# Create a Virtual Network
# Implicit Dependency: By referencing 'azurerm_resource_group.network-rg.location' and '.name',
# Terraform automatically knows it must create the Resource Group BEFORE creating this Virtual Network.
resource "azurerm_virtual_network" "network-vnet" {
  name                = "network-vnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.network-rg.location
  resource_group_name = azurerm_resource_group.network-rg.name
}

# Create the first Subnet within the Virtual Network
resource "azurerm_subnet" "network-subnet1" {
  name                 = "network-subnet1"
  resource_group_name  = azurerm_resource_group.network-rg.name
  virtual_network_name = azurerm_virtual_network.network-vnet.name
  address_prefixes     = ["10.0.0.0/24"]
}

# Create a Network Security Group (NSG) for the first subnet
# NSGs act as a virtual firewall to filter traffic.
resource "azurerm_network_security_group" "network-nsg1" {
  name                = "network-nsg1"
  location            = azurerm_resource_group.network-rg.location
  resource_group_name = azurerm_resource_group.network-rg.name

  # Security Rule: Allow SSH (Port 22)
  # Priority: Rules are processed in order. Lower numbers (e.g., 100) are processed 
  # before higher numbers. Once a rule matches, processing stops.
  security_rule {
    name                       = "rule1"
    priority                   = 100
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  # Security Rule: Allow HTTP (Port 80)
  security_rule {
    name                       = "rule2"
    priority                   = 200
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "80"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

# Associate the NSG with Subnet 1
# This binds the firewall rules defined in 'network-nsg1' to 'network-subnet1'.
resource "azurerm_subnet_network_security_group_association" "network-nsg1-assoc" {
  # The 'id' is an exported attribute generated by Azure after creation.
  # It is not defined manually in the resource block but is available for reference.
  subnet_id                 = azurerm_subnet.network-subnet1.id
  network_security_group_id = azurerm_network_security_group.network-nsg1.id
}

# Create the second Subnet
resource "azurerm_subnet" "network-subnet2" {
  name                 = "network-subnet2"
  resource_group_name  = azurerm_resource_group.network-rg.name
  virtual_network_name = azurerm_virtual_network.network-vnet.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Create a Network Security Group (NSG) for the second subnet
resource "azurerm_network_security_group" "network-nsg2" {
  name                = "network-nsg2"
  location            = azurerm_resource_group.network-rg.location
  resource_group_name = azurerm_resource_group.network-rg.name

  # Security Rule: Allow RDP (Port 3389) for Windows remote access
  security_rule {
    name                       = "rule1"
    priority                   = 100
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "3389"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  # Security Rule: Allow WinRM (Port 5985) for Windows management
  security_rule {
    name                       = "rule2"
    priority                   = 200
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "5985"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

# Associate the NSG with Subnet 2
resource "azurerm_subnet_network_security_group_association" "network-nsg2-assoc" {
  subnet_id                 = azurerm_subnet.network-subnet2.id
  network_security_group_id = azurerm_network_security_group.network-nsg2.id
}
